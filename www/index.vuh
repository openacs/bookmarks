#
# Outputs an html file in the format used by netscape/mozilla
# for it's bookmarks menu
#

# index.vuh created from the original registered proc
# because registered procs seem not to play with server
# 404s

# Mark Aufflick (mark@pumptheory.com)
# 26 September 2003
# $Id$

set user_id [ad_maybe_redirect_for_registration]

set package_id [ad_conn package_id]

set root_folder_id [bm_get_root_folder_id $package_id $user_id]

set name [db_string name "
    select first_names||' '||last_name as name 
    from   cc_users 
    where  user_id = :user_id"]

set folder_list 0
set previous_folder_p "f"
set indent 1
set indent_str "    "
set i_str [string repeat $indent_str $indent]

db_foreach bm_info { } {

    if { [string equal $folder_list 0] } {
	lappend folder_list $parent_id
    } else {
	set previous_parent_id [lindex $folder_list end]	
	if {$parent_id != $previous_parent_id} {
	    set parent_location [lsearch -exact $folder_list $parent_id]

	    if {$parent_location==-1} {
		lappend folder_list $parent_id
		append bookmark_html "$i_str<DL><p>\n"
		incr indent
		set i_str [string repeat $indent_str $indent]
	    } else {
		set drop [expr [llength $folder_list]-$parent_location]
		set folder_list [lrange $folder_list 0 $parent_location]
		for {set i 1} {$i<$drop} {incr i} {
		    incr indent -1
		    set i_str [string repeat $indent_str $indent]
		    append bookmark_html "$i_str</DL><p>\n\n"
		}
	    }
	} elseif { [string equal $folder_p "t"] && [string equal $previous_folder_p "t"] } {
	    # The previous folder was empty
	    append bookmark_html "$i_str<DL><p>\n$i_str</DL><p>\n"
	}
    }

    if {$folder_p=="t"} {
	if {$local_title eq "Personal Toolbar Folder"} {
	    # Maintain special tags for Personal Toolbar Folder
	    append bookmark_html "$i_str<DT><H3 LAST_MODIFIED=\"[ns_time]\" PERSONAL_TOOLBAR_FOLDER=\"true\" ID=\"NC:PersonalToolbarFolder\">$local_title</H3>\n"
	} else {
	    append bookmark_html "$i_str<DT><H3 ADD_DATE=\"[ns_time]\">$local_title</H3>\n"
	}
    } else {
	append bookmark_html "$i_str<DT><A HREF=\"$complete_url\" ADD_DATE=\"[ns_time]\" LAST_VISIT=\"0\" LAST_MODIFIED=\"0\">$local_title</A>\n"
    }

    set previous_folder_p $folder_p
}

set html "<!DOCTYPE NETSCAPE-Bookmark-file-1>
<!-- This is an automatically generated file.
     It will be read and overwritten.
     Do Not Edit! -->
<TITLE>Bookmarks for $name</TITLE>
<H1>Bookmarks for $name</H1>
<DL><p>
$bookmark_html
</DL><p>
"

    
doc_return  200 text/html $html
